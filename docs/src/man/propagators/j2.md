# [J2 Analytical Orbit Propagator](@id J2_Propagator)

```@meta
CurrentModule = SatelliteToolboxPropagators
DocTestSetup = quote
    using SatelliteToolboxPropagators
end
```

The J2 analytical orbit propagator considers only the J2 perturbation term of the
geopotential field to obtain a simple analytic solution for propagating the mean orbital
elements. It has low precision but is extremely useful during mission design. For example,
when designing a Sun-Synchronous orbit for a remote sensing mission, the satellite usually
has a propulsion subsystem to maintain its orbit. Hence, the J2 propagator solution can be
used as the nominal orbit through the mission lifetime to perform initial studies.

## Algorithm

The algorithm implemented here is based on **[1, 2]**. This version takes into account only
the secular effects in the orbit elements.

After neglecting all terms with higher order than J2, we obtain that only the right
accession of the ascending node (RAAN), the argument of perigee, and mean motion are
perturbed by the geopotential field with secular effects. However, we also include the
possibility of inputting the mean motion time derivatives to consider effects like energy
dissipation by air drag.  Hence, the algorithm propagates the mean elements using the
following equations:

```math
\begin{aligned}
  a(t)       &= a_0 - \frac{2}{3} \cdot \frac{\dot{n}}{n_0} \cdot a_0 \cdot \left(t - t_0\right) \\
  e(t)       &= e_0 - \frac{2}{3} \cdot \left(1 - e_0\right) \cdot \frac{\dot{n}}{n_0} \cdot \left(t - t_0\right) \\
  \bar{n}(t) &= n_0 \cdot \left[1 + \frac{3}{4} \cdot J_2 \cdot \left(\frac{R_0}{p_0}\right)^2 \cdot \sqrt{1 - e_0^2} \cdot \left(2 - 3\sin{i_0}^2\right)\right] \cdot \left(t - t_0\right) \\
  \Omega(t)  &= \Omega_0 - \frac{2}{3} \cdot J_2 \cdot \left(\frac{R_0}{p_0}\right)^2 \cdot \bar{n} \cdot \cos{i_0} \cdot \left(t - t_0\right) \\
  \omega(t)  &= \omega_0 + \frac{3}{4} \cdot J_2 \cdot \left(\frac{R_0}{p_0}\right)^2 \cdot \left(4 - 5\sin{i_0}^2\right) \cdot \left(t - t_0\right) \\
  M(t)       &= M_0 + \bar{n} \cdot \left(t - t_0\right) + \frac{\dot{n}}{2} \cdot \left(t - t_0\right)^2 + \frac{\ddot{n}}{6} \cdot \left(t - t_0\right)^3 \\
\end{aligned}
```

where the subscript ``_0`` indicates the initial element, ``t_0`` is the initial mean
elements' epoch, ``a`` is the mean semi-major axis, ``n`` is mean motion, ``\bar{n}`` is the
perturbed mean motion, ``e`` is the eccentricity, ``p`` is the *semi-latus rectum*, ``R_0``
is the Earth's equatorial radius, ``\Omega`` is the RAAN, and ``M`` is the mean anomaly.

## Initialization

We can initialize the J2 analytical orbit propagator with the following function:

```julia
function Propagators.init(Val(:J2), orb₀::KeplerianElements, dn_o2::Number = 0, ddn_o6::Number = 0; kwargs...)
```

which creates a J2 propagator structure [`OrbitPropagatorJ2`](@ref) with the mean Keplerian
elements `orb₀`. It supports the following positional arguments:

- `orb₀::KeplerianElements`: Initial mean Keplerian elements [SI units].
- `dn_o2::Number`: First time derivative of the mean motion divided by two [rad/s^2].
    (**Default** = 0)
- `ddn_o6::Number`: Second time derivative of the mean motion divided by six [rad/s^3].
    (**Default** = 0)
    
The following keyword selects the gravitational constants for the propagation algorithm:

- `j2c::J2PropagatorConstants`: J2 orbit propagator constants (see
  [`J2PropagatorConstants`](@ref)). (**Default** = `j2c_egm2008`)

This package contains some pre-built propagation constants for this propagator:

| **J2 Propagator Constant** | **Description**                  | **Type**  |
|---------------------------:|:---------------------------------|:----------|
|              `j2c_egm2008` | EGM-2008 gravitational constants | `Float64` |
|          `j2c_egm2008_f32` | EGM-2008 gravitational constants | `Float32` |
|              `j2c_egm1996` | EGM-1996 gravitational constants | `Float64` |
|          `j2c_egm1996_f32` | EGM-1996 gravitational constants | `Float32` |
|                `j2c_jgm02` | JGM-02 gravitational constants   | `Float64` |
|            `j2c_jgm02_f32` | JGM-02 gravitational constants   | `Float32` |
|                `j2c_jgm03` | JGM-03 gravitational constants   | `Float64` |
|            `j2c_jgm03_f32` | JGM-03 gravitational constants   | `Float32` |

!!! note
    The type used in the propagation will be the same as used to define the constants in the
    structure `j2c`.

```jldoctest
julia> orb = KeplerianElements(
           date_to_jd(2023, 1, 1, 0, 0, 0),
           7190.982e3,
           0.001111,
           98.405 |> deg2rad,
           100    |> deg2rad,
           90     |> deg2rad,
           19     |> deg2rad
       )
KeplerianElements{Float64, Float64}:
           Epoch :    2.45995e6 (2023-01-01T00:00:00)
 Semi-major axis : 7190.98     km
    Eccentricity :    0.001111
     Inclination :   98.405    °
            RAAN :  100.0      °
 Arg. of Perigee :   90.0      °
    True Anomaly :   19.0      °

julia> orbp = Propagators.init(Val(:J2), orb; j2c = j2c_jgm03)
OrbitPropagatorJ2{Float64, Float64}:
   Propagator name : J2 Orbit Propagator
  Propagator epoch : 2023-01-01T00:00:00
  Last propagation : 2023-01-01T00:00:00
```

## Fitting Mean Elements

We can use the function:

```julia
function Propagators.fit_mean_elements(::Val{:J2}, vjd::AbstractVector{Tjd}, vr_i::AbstractVector{Tv}, vv_i::AbstractVector{Tv}; kwargs...) -> KeplerianElements{Float64, Float64}, SMatrix{6, 6, Float64}
```

to fit a set of mean Keplerian elements for the J2 orbit propagator using the osculating
elements represented by a set of position vectors `vr_i` [m] and a set of velocity vectors
`vv_i` [m / s] represented in an inertial reference frame at instants in the array `vjd`
[Julian Day].

It returns the fitted Keplerian elements and the final covariance matrix of the least-square
algorithm.

!!! note
    This algorithm version will allocate a new J2 propagator with the default constants
    `j2c_egm2008`. If another set of constants are required, use the function
    [`Propagators.fit_mean_elements!`](@ref) instead.
    
The following keywords are available to configure the fitting process:

- `atol::Number`: Tolerance for the residue absolute value. If the residue is lower than
    `atol` at any iteration, the computation loop stops. (**Default** = 2e-4)
- `rtol::Number`: Tolerance for the relative difference between the residues. If the
    relative difference between the residues in two consecutive iterations is lower than
    `rtol`, the computation loop stops. (**Default** = 2e-4)
- `initial_guess::Union{Nothing, KeplerianElements}`: Initial guess for the mean elements
    fitting process. If it is `nothing`, the algorithm will obtain an initial estimate from
    the osculating elements in `vr_i` and `vv_i`. (**Default** = nothing)
- `jacobian_perturbation::Number`: Initial state perturbation to compute the
    finite-difference when calculating the Jacobian matrix. (**Default** = 1e-3)
- `jacobian_perturbation_tol::Number`: Tolerance to accept the perturbation when calculating
    the Jacobian matrix. If the computed perturbation is lower than
    `jacobian_perturbation_tol`, we increase it until it absolute value is higher than
    `jacobian_perturbation_tol`. (**Default** = 1e-7)
- `max_iterations::Int`: Maximum number of iterations allowed for the least-square fitting.
    (**Default** = 50)
- `mean_elements_epoch::Number`: Epoch for the fitted mean elements.
    (**Default** = vjd[end])
- `verbose::Bool`: If `true`, the algorithm prints debugging information to `stdout`.
    (**Default** = true)
- `weight_vector::AbstractVector`: Vector with the measurements weights for the least-square
    algorithm. We assemble the weight matrix `W` as a diagonal matrix with the elements in
    `weight_vector` at its diagonal. (**Default** = `@SVector(ones(Bool, 6))`)
    
```julia
julia> vr_i = [
           [-6792.402703741442, 2192.6458461287293, 0.18851758695295118]  .* 1000,
           [-1781.214419290065, 1619.7795321872854, 6707.771633846665]    .* 1000,
           [ 5693.643675547716, -1192.342828671633, 4123.976025977494]    .* 1000,
           [ 5291.613719530499, -2354.5417593130833, -4175.561367156414]  .* 1000,
           [-2416.3705905186903, -268.74923235392623, -6715.411357310478] .* 1000,
           [-6795.043410709359, 2184.4414321930635, -0.4327055325971031]  .* 1000,
       ];

julia> vv_i = [
           [0.3445760107690598, 1.0395135806993514, 7.393686131436984]    .* 1000,
           [6.875680282038698, -1.864319399615942, 2.270603214569518]     .* 1000,
           [3.8964090757666496, -2.1887896252945875, -5.9960180359219075] .* 1000,
           [-4.470258022565413, 0.5119576359985208, -5.9608372367141635]  .* 1000,
           [-6.647358060413909, 2.495415251255861, 2.292118747543002]     .* 1000,
           [0.3427096905434428, 1.040125572862349, 7.3936887585116855]    .* 1000,
       ];

julia> vjd = [
           2.46002818657856e6
           2.460028200467449e6
           2.460028214356338e6
           2.4600282282452267e6
           2.4600282421341157e6
           2.4600282560230047e6
       ];

julia> orb, P = Propagators.fit_mean_elements(Val(:J2), vjd, vr_i, vv_i)
ACTION:   Fitting the mean elements for the J2 propagator.
           Iteration        Position RMSE        Velocity RMSE           Total RMSE       RMSE Variation
                                     [km]             [km / s]                  [ ]
PROGRESS:         23               4.3413           0.00540076              4341.31         -2.90721e-06 %

(KeplerianElements{Float64, Float64}: Epoch = 2.46003e6 (2023-03-24T18:08:40.388), [0.16604846233666615 0.06643574144803302 … -3.85541368066423e-5 0.000124032254172014; 0.0664357414470214 0.26633448262787296 … -1.7943612056596758e-5 -1.9567956793856743e-5; … ; -3.855413680493565e-5 -1.7943612058983507e-5 … 4.3971984339116176e-7 -8.092704691911699e-8; 0.0001240322541726098 -1.9567956793417353e-5 … -8.092704692135158e-8 1.2451922454639337e-7])

julia> orb
KeplerianElements{Float64, Float64}:
           Epoch :    2.46003e6 (2023-03-24T18:08:40.388)
 Semi-major axis : 7131.63       km
    Eccentricity :    0.00114299
     Inclination :   98.4366     °
            RAAN :  162.177      °
 Arg. of Perigee :  101.286      °
    True Anomaly :  258.689      °
```

## References

- **[1]** **Vallado, D. A** (2013). *Fundamentals of Astrodynamics and Applications*. 4th
  ed. **Microcosm Press**, Hawthorn, CA, USA.
- **[2]** **Wertz, J. R** (1978). *Spacecraft Attitude Determination and Control*. **Kluwer
  Academic Publishers**, Dordrecht, The Netherlands.
